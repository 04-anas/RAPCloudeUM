managed implementation in class zbp_i_purchaseheader_tp unique;
strict ( 2 );
with draft;
define behavior for ZI_PurchaseHeader_TP //alias <alias_name>
persistent table zpo_header_db2
draft table zpo_header_db2_d
lock master
total etag last_change_at
authorization master ( instance )
early numbering   // to genrate key field and assign in db table
etag master ChangedDateTime
{

  field ( readonly ) PurchaseOrderNumber , PurchaseTotalPrice ,criticalvalue;
  field ( mandatory ) Description;
  create;
  update;
  delete;
  association _PurchaseItem { create; with draft; }
  association _Po_attachment { create; with draft; }
  action copy result[1] $self ;
  action withdrawApproval result[1] $self ;
  validation validateStatus on save { create; field po_status ;}
  draft action Edit;      // neccessory action to add for draft are bellow
  draft action Resume;
  draft action Discard;
  draft action  Activate ;
  draft determine action Prepare
  {
   validation validateStatus  ;
  }
  mapping for zpo_header_db2
    {
      PurchaseOrderNumber = po_order;
      Description         = po_desc;
      OrderType           = po_type;
      CompnyCode          = comp_code;
      Organization        = po_org;
      Supplier_id         = supplier;
      po_status           = po_status;
      Imageurl            = imageurl;
      CreateBy            = create_by;
      CreatedDateTime     = created_date_time;
      ChangedDateTime     = changed_date_time;
      LocalLastChangedBy  = local_last_changed_by;
       last_change_at     =  last_change_at;
    }
}

define behavior for ZI_PURCHASE_ITEAM_TP //alias <alias_name>
implementation in class zbp_i_purchaseItem_tp unique
persistent table zpo_iteam_db
draft table zpo_iteam_db_d
lock dependent by _PurchaseHeader
early numbering   // to genrate key field and assign in db table
authorization dependent by _PurchaseHeader
//etag master <field_name>
{
  update;
  delete;
  field ( readonly ) PurchaseOrderNumber, PurchaseIteam,IteamPrice ;
  association _PurchaseHeader { with draft; }
  determination SetPurchaseStatus on save {create; field ProductPrice,PriceUnit,OrderQunty,OrderUnit;}
  mapping for zpo_iteam_db
    {
      PurchaseOrderNumber = po_order;
      PurchaseIteam       = po_item;
      ShortText           = short_text;
      Material            = material;
      Plant               = plant;
      MaterialGroup       = mat_group;
      OrderQunty          = order_qunty;
      OrderUnit           = order_unit;
      ProductPrice          = product_price;
      PriceUnit           = price_unit;
      LocalLastChangedBy  = local_last_changed_by;
      LocalLastChangedAt  = local_last_changed_at;
    }

}
define behavior for ZI_PO_ATTACHMENT1
implementation in class zbp_i_purchaseattach_tp unique
persistent table zpo_attachment1
draft table zpo_attach1_d
lock dependent by _PurchaseHeader
early numbering   // to genrate key field and assign in db table
authorization dependent by _PurchaseHeader
etag master LastChangeAt
{
  update;
  delete;
  field ( readonly ) Purchaseordernumber, AttachId ;
  association _PurchaseHeader { with draft; }
  mapping for zpo_attachment1
  {

      Purchaseordernumber = purchaseordernumber;
      AttachId = attach_id;
      Attachment = attachment;
      Mimetype = mimetype;
      Filename = filename;
      LastChangeAt = last_change_at;
  }
  }